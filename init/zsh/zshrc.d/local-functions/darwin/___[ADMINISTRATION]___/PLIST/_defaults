#compdef defaults

##########################################################################################
#                                                                                        #
#  ZaemonSH created by :                                                                 #
#  Zucchelli Bryan && @N@TOME && Admiral Gr@ss H0pper on Oct 13, 2016.                   #
#  Copyright (c) 2019. All rights reserved.                                              #
#  Inspired by zsh-templates-osx created by :                                            #
#  William Scott on May 11, 2007.                                                        #
#  Copyright (c) 2007. All rights reserved.                                              #
#                                                                                        #
#  This program is free software; you can redistribute it and/or modify it under         #
#  the terms of the GNU General Public License as published by the Free Software         #
#  Foundation; either version 2 of the License, or (at your option) any later version.   #
#                                                                                        #
#  This program is distributed in the hope that it will be useful, but WITHOUT           #
#  ANY WARRANTY ; without even the implied warranty of MERCHANTABILITY or                #
#  FITNESS FOR A PARTICULAR PURPOSE.                                                     #
#  See the GNU General Public License for more details.                                  #
#                                                                                        #
#  You should have received a copy of the GNU General Public License                     #
#  along with this program ; if not, write to the Free Software Foundation,              #
#  Inc., 51 Franklin Street, Fifth Floor, Boston,  MA 02110-1301 USA                     #
#                                                                                        #
#  cf. URL:   http://www.fsf.org/licensing/licenses/gpl.html                             #
#                                                                                        #
##########################################################################################

#----------------------------------------------------------------------------------------#
#                                     DESCRIPTION
#----------------------------------------------------------------------------------------#
# _defaults :
# Author:  Motoi Washida 
# Feb 18 2005 CVS version


#----------------------------------------------------------------------------------------#
#                                        TODO
#----------------------------------------------------------------------------------------#

#----------------------------------------------------------------------------------------#
#                                       CHANGES
#----------------------------------------------------------------------------------------#
# 19/10/2017 :
#  â€¢ Modify script presentation.

#----------------------------------------------------------------------------------------#
#                                       CONFIG
#----------------------------------------------------------------------------------------#
local _defaults_domains _defaults_keys

#----------------------------------------------------------------------------------------#
#                                      FUNCTIONS
#----------------------------------------------------------------------------------------#
_defaults_domains(){
  if [[ "`eval echo $PREFIX`" != [/~]* ]]; then
    local str="$(_call_program domains defaults domains 2>/dev/null)"
    local expl
    local -a list
    list=( ${(s/, /)str} -g -globalDomain )
    _wanted domains expl 'defaults database domain' \
  compadd -M 'r:|.=* r:|=*' -a list
  else
    _files -g '*.plist(e:"reply=\${REPLY%.plist}":)'
  fi
}

_defaults_keys(){
  local ks
  ks=(${${${${(M)${(f)"$(defaults read "$words[2]" 2>/dev/null)"}:#    [^ ]*=*}#    }%% = *}:Q})
  local expl
  _wanted keys expl 'key' compadd "$ks[@]"
}

_defaults(){
  local -a _1st_arguments
  _1st_arguments=( read read-type write rename delete domains find help )

  local curcontext="$curcontext" state line expl

  _arguments -A '-*' \
    '(-currentHost)-host:host:_hosts' \
    '(-host)-currentHost' \
    '*::command:->subcmd' && return 0

  if (( CURRENT == 1 )); then
    _describe -t commands "command" _1st_arguments
    return
  fi

  case $words[1] in
    read|read-type|delete)
      _arguments \
        "(1)-app:application:_mac_applications" \
        "(-app)1:domain:_defaults_domains" \
        "2:keys:_defaults_keys"
      ;;
    write)
      _arguments \
        "(1)-app:application:_mac_applications" \
        "(-app)1:domain:_defaults_domains" \
        "2:key:_defaults_keys" \
        "*::value"
      ;;
    rename)
      _arguments \
        "(1)-app:application:_mac_applications" \
        "(-app)1:domain:_defaults_domains" \
        "2:old key:_defaults_keys" \
        "3:new key:_defaults_keys"
      ;;
    domains|help)
      ;;
    find)
      _message word
      ;;
    *)
      _message "unknown defaults command: $words[1]"
      ;;
  esac
}

#----------------------------------------------------------------------------------------#
#                                      COMPLETION
#----------------------------------------------------------------------------------------#
_defaults "$@"

